<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ISMTelemetryEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.
/// <summary>
/// Initalizes the telemetry parameters for all child classes of ISMTelemetryBase
/// </summary>
internal final class ISMTelemetryEventHandler
{
    

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>sysIntParameters_OnInitializing</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the telemetry parameters for all child classes of ISMTelemetryBase
    /// </summary>
    /// <param name="sender">sender</param>
    /// <param name="e">event arguments</param>
    [SuppressBPWarning('BPParameterNotUsed', 'Parameter required by the event interface.'),
    FormEventHandler(formStr(SysIntParameters), FormEventType::Initializing)]
    public static void sysIntParameters_OnInitializing(xFormRun sender, FormEventArgs e)
    {
        #AOT

        //set parent class and method you want to investigate
        ClassName baseClassName = classStr(ISMTelemetryBase);

        SysDictClass dictClass = new SysDictClass(className2Id(baseClassName));
        List extendedClassList = dictClass.extendedBy();

        //loop through all child classes
        if (extendedClassList.elements())
        {
            ListEnumerator listEnumerator = extendedClassList.getEnumerator();

            while (listEnumerator.moveNext())
            {
                ClassId childClassId = listEnumerator.current();
                TreeNode childClassNode = TreeNode::findNode(#ClassesPath + #AOTDelimiter + classId2Name(childClassId));

                ISMTelemetryParameterSetup param = ISMTelemetryParameterSetup::find(childClassNode.AOTname());

                if (!param)
                {
                    ttsbegin;
                    param.TelemetryClass = childClassNode.AOTname();
                    param.IsEnabled = NoYes::Yes;
                    param.SeverityLevel = ISMTelemetrySeverityLevel::Information;
                    param.insert();
                    ttscommit;
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>ISMTelemetryConfiguration_OnClicked</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens the telemetry configuration dialog for the selected telemetry parameter setup.
    /// </summary>
    /// <param name="sender">The sender of the event.</param>
    /// <param name="e">Event arguments associated with the event.</param>
    [
        SuppressBPWarning('BPParameterNotUsed', 'Parameter required by the event interface.'),
        FormControlEventHandler(
            formControlStr(SysIntParameters, ISMTelemetryConfiguration), 
            FormControlEventType::Clicked)
    ]
    public static void ISMTelemetryConfiguration_OnClicked(
        FormControl sender, 
        FormControlEventArgs e)
    {
        FormRun formRun = sender.formRun();
        FormDataSource formDataSource = 
            formRun.dataSource(formDataSourceStr(SysIntParameters, ISMTelemetryParameterSetup));
        ISMTelemetryParameterSetup telemetryParameterSetup = formDataSource.cursor();
        DictClass dictClass = new DictClass(className2Id(telemetryParameterSetup.TelemetryClass));
        ISMTelemetryBase telemetry = dictClass.makeObject();
        if (ISMTelemetryConfigurationDialogController::telemetryConfigurationDialog(telemetry))
        {
            telemetryParameterSetup.TelemetryConfiguration = telemetry.getConfigurationContainer();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>